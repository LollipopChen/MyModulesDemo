apply plugin: 'com.android.application'
// 应用WMRouter插件
apply plugin: 'WMRouter'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.mymodulesdemo"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath = true
            }
        }

        //MultiDex分包方法
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    //资源文件分类
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            res.srcDirs = [
                    'src/main/res/drawable/background',
                    'src/main/res/drawable/icon',
                    'src/main/res/drawable',

                    'src/main/res/layout/me',
                    'src/main/res/layout/main',
                    'src/main/res/layout/search',
                    'src/main/res/layout/other',
                    'src/main/res/layout',
                    'src/main/res'
            ]
        }
    }

    //数据绑定开启
    dataBinding {
        enabled = true
    }
    //JDK版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //引用module
    //公共module
    implementation project(':lib_base')
    if (!Boolean.valueOf(rootProject.ext.isModule)) {
        //登录模块
        implementation project(':login')
    }

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    /* Leaks */
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.2'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    /*路由*/
    annotationProcessor 'com.sankuai.waimai.router:compiler:1.2.0'

    /*多功能选择器*/
    implementation 'com.contrarywind:Android-PickerView:4.1.8'
}
